plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'maven-publish'
    id 'java-library'
    id 'jacoco'
}

repositories {
    jcenter()
}

dependencies {
	api 'com.squareup.retrofit2:retrofit:2.3.0'

	implementation 'com.jakewharton.retrofit:retrofit2-reactor-adapter:2.1.0'
	implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'
	implementation 'org.projectlombok:lombok:1.16.18'

	testImplementation 'junit:junit:4.12'
	testImplementation 'io.projectreactor:reactor-test:3.1.5.RELEASE'
}

group = 'telegram-bot'
version = '0.1.0-SNAPSHOT'

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['telegram-bot-java-api']
	publish = true
	pkg {
		repo = 'telegram-bot'
		name = 'telegram-bot-java-api'
		licenses = ['EPL-2.0']
		vcsUrl = 'git@github.com:SimonScholz/telegram-bot-java-api.git'
		githubRepo = 'https://github.com/SimonScholz/telegram-bot-java-api'
		publicDownloadNumbers = true
		version {
        	name = "${version}"
        	desc = 'This repo contains java artifacts to implement bot for the telegram messenger'
        	released  = new Date()
        	vcsTag = "${version}"
 	   }
	}
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "Eclipse Public License, Version 2.0"
            url "https://www.eclipse.org/org/documents/epl-2.0/EPL-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "simon-scholz"
            name "Simon Scholz"
            email "simon.scholz@vogella.com"
        }
    }
    
    scm {
       url "https://github.com/SimonScholz/telegram-bot-java-api"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'telegram-bot'
            artifactId 'telegram-bot-java-api'
            version "${version}"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'This repo contains java artifacts to implement bot for the telegram messenger')
                root.appendNode('name', 'telegram-bot-java-api')
                root.appendNode('url', 'https://github.com/SimonScholz/telegram-bot-java-api')
                root.children().last() + pomConfig
            }
        }
    }
}

// Jacoco and code coverage setup
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

check.dependsOn jacocoTestReport
